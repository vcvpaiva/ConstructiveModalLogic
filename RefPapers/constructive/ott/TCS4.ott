metavar termvar, x , y ::=
  {{ tex \mathit{[[termvar]]} }} {{ com  term variable  }}

indexvar index, i , j , k ::=

grammar
  term, t :: 't_' ::=                                         {{ com term }}
    | x                          ::   :: var                     {{ com variable }}
    | unit                       ::   :: unit                    {{ com unit }}
    | contra                     ::   :: contra                  {{ com contradiction }}
    | ( t1 , t2 )                ::   :: pair                    {{ com pair }}
    | fst t                      ::   :: first                   {{ com first projection }}
    | snd t                      ::   :: second                  {{ com second projection }}
    | inj1 t                     ::   :: firstInj                {{ com first injection }}
    | inj2 t                     ::   :: secondInj               {{ com second injection }}
    | case t of x . t1 , y . t2  ::   :: case                    {{ com sum case }}
    {{tex \mathsf{case}\,[[t]]\,\mathsf{of}\,[[x]].[[t1]],[[y]].[[t2]] }}
    | \ x : T . t                ::  :: lambdas                  {{ com unary functions }}
    {{tex \lambda [[x]] : [[T]].[[t]] }}
    | t1 t2                      ::  :: application              {{ com function application }}
    | Box t                      ::  :: PNecessity               {{ com past necessity functor }}
    | Dia t                      ::  :: PPossibility             {{ com past possibility functor }}
    | BBox t                     ::  :: Necessity                {{ com necessity functor }}
    | BDia t                     ::  :: Possibility              {{ com possibility functor }}
    | letBox t1 = t2 in t3       ::  :: LetPNecessity            {{ com past necessity elim }}
    {{tex \mathsf{let}\,\Box [[t1]] = [[t2]]\,\mathsf{in}\,[[t3]] }}
    | letBBox t1 = t2 in t3      ::  :: LetNecessity            {{ com necessity elim }}
    {{tex \mathsf{let}\,\blacksquare [[t1]] = [[t2]]\,\mathsf{in}\,[[t3]] }}
    | letDia t1 = t2 in t3       ::  :: LetPPossibility           {{ com past possibility elim }}
    {{tex \mathsf{let}\,\Diamond [[t1]] = [[t2]]\,\mathsf{in}\,[[t3]] }}
    | letBDia t1 = t2 in t3      ::  :: LetPossibility           {{ com possibility elim }}
    {{tex \mathsf{let}\,\blacklozenge [[t1]] = [[t2]]\,\mathsf{in}\,[[t3]] }}
    | [ t1 / x ] t2              :: M :: Sub
    | ( t )                      :: S :: Paren

  form, type, A, B, C, T :: 'T_' ::=                   {{ com formula and type }}
    | True             ::   :: True                    {{ com true or the unit type }}
    | False            ::   :: False                   {{ com false or the empty type }}
    | Box A            ::   :: PNecessity               {{ com past necessity }}
    | BBox A           ::   :: Necessity              {{ com necessity }}
    | Dia A            ::   :: PPossibility             {{ com past possibility }}
    | BDia A           ::   :: Possibility            {{ com possibility }}
    | A x B            ::   :: Conjunction             {{ com conjunction }}
    {{tex [[A]] \land [[B]] }}
    | A + B            ::   :: Disjunction             {{ com disjunction }}
    {{tex [[A]] \lor [[B]] }}
    | A -> B           ::   :: Arrow                   {{ com implication }}

  G {{ tex \Gamma }}, D {{tex \Delta }} :: G_ ::=      {{ com type context }}
    | .                   ::   :: em                      {{ com empty context }}
    | A                   ::   :: elF                     {{ com formula el }}
    | x : T               ::   :: elT                     {{ com typed el }}
    | G , G'              ::   :: append                  {{ com append }}

  terminals :: 'terminals_' ::=
    | fst                 ::   :: First
    {{tex \mathsf{fst}\, }}
    | snd                 ::   :: Second
    {{tex \mathsf{snd}\, }}
    | inj1                ::   :: FirstInj
    {{tex \mathsf{inj}_1\, }}
    | inj2                ::   :: SecondInj
    {{tex \mathsf{inj}_2\, }}    
    | .                   ::   :: Empty
    {{tex \emptyset }}
    | True                ::   :: True
    {{tex \top }}
    | False               ::   :: False
    {{tex \perp }}
    | |-                  ::   :: turnstile
    {{ tex \vdash }}
    | Box                 ::   :: Box
    {{tex \Box }}
    | BBox                ::   :: BBox
    {{ tex \blacksquare }}
    | Dia                 ::   :: Dia
    {{tex \Diamond }}
    | BDia                ::   :: BDia
    {{tex \mathbin{\blacklozenge} }}
    | ->                  ::   :: Arrow
    {{tex \to }}
    | &&                  :: M :: Quad
    {{tex \quad }}
    | unit                ::   :: unit                   
    {{tex \mathsf{unit} }}
    | contra              ::   :: contra
    {{tex \mathsf{contra} }}
    | ~>                  ::   :: red
    {{tex \rightsquigarrow }}

  formula :: 'formula_' ::=
    | judgement            ::   :: judgement
    | formula1 && formula2 :: M :: quad
    | ( formula )          :: S :: parens

defns
  Jtype :: '' ::=

defn
    G ; D |- A :: :: form :: L_    
by

  ---------- :: ax
  G;D,A |- A

  ---------- :: bax
  G,A;. |- A

  ------------- :: true
  G ; D |- True

  ----------------- :: false
  G ; D, False |- A

  G ; D |- A && G ; D |- B
  ------------------------- :: conjI
  G ; D |- A x B

  G ; D |- A x B
  -------------- :: conjE1
  G ; D |- A

  G ; D |- A x B
  -------------- :: conjE2
  G ; D |- B

  G ; D |- A
  -------------- :: disjI1
  G ; D |- A + B

  G ; D |- B
  -------------- :: disjI2
  G ; D |- A + B

  G ; D, A |- C
  G ; D, B |- C && G ; D |- A + B
  ------------------------------- :: disjE
  G ; D |- C

  G ; D, A |- B
  --------------- :: impI
  G ; D |- A -> B
 
  G ; D |- A -> B && G ; D |- A
  ----------------------------- :: impE
  G ; D |- B

  G ; . |- A
  -------------- :: boxI
  G ; D |- Box A

  G ; D |- Box A && G, A; D |- B
  ------------------------------ :: boxE
  G ; D |- B

  G ; D |- A
  --------------- :: bdiaI
  G ; D |- BDia A

  G ; D |- BDia A && G ; A |- BDia B
  ---------------------------------- :: bdiaE
  G ; D |- BDia A

  G ; . |- A
  --------------- :: bboxI
  G ; D |- BBox A

  G ; D |- BBox A && G, A; D |- B
  ------------------------------- :: bboxE
  G ; D |- B

  G ; D |- A
  -------------- :: diaI
  G ; D |- Dia A

  G ; D |- Dia A && G ; A |- Dia B
  -------------------------------- :: diaE
  G ; D |- Dia B


defn
  G ; D |- t : A :: :: type :: ty_   
by

  ------------------- :: ax
  G;D, x : A |- x : A

  ------------------ :: bax
  G,x : A;. |- x : A

  -------------------- :: true
  G ; D |- unit : True

  ------------------------------ :: false
  G ; D, x : False |- contra : A

  G ; D |- t1 : A && G ; D |- t2 : B
  ---------------------------------- :: conjI
  G ; D |- (t1,t2) : A x B

  G ; D |- t : A x B
  ------------------ :: conjE1
  G ; D |- fst t : A

  G ; D |- t : A x B
  ------------------ :: conjE2
  G ; D |- snd t : B

  G ; D |- t : A
  ----------------------- :: disjI1
  G ; D |- inj1 t : A + B

  G ; D |- t : B
  ----------------------- :: disjI2
  G ; D |- inj2 t : A + B

  G ; D, x : A |- t1 : C
  G ; D, x : B |- t2 : C && G ; D |- t : A + B
  -------------------------------------------- :: disjE
  G ; D |- case t of x.t1,x.t2 : C

  G ; D, x : A |- t : B
  ------------------------ :: impI
  G ; D |- \x:A.t : A -> B
 
  G ; D |- t1 : A -> B && G ; D |- t2 : A
  --------------------------------------- :: impE
  G ; D |- t1 t2 : B

  G ; . |- t : A
  ---------------------- :: boxI
  G ; D |- Box t : Box A

  G ; D |- t1 : Box A && G, x : A; D |- t2 : B
  -------------------------------------------- :: boxE
  G ; D |- letBox x = t1 in t2 : B

  G ; D |- t : A
  ------------------------ :: bdiaI
  G ; D |- BDia t : BDia A

  G ; D |- t1 : BDia A && G ; x : A |- t2 : BDia B
  ------------------------------------------------ :: bdiaE
  G ; D |- letBDia x = t1 in t2 : BDia B

  G ; . |- t : A
  ------------------------ :: bboxI
  G ; D |- BBox t : BBox A

  G ; D |- t1 : BBox A && G, x : A; D |- t2 : B
  --------------------------------------------- :: bboxE
  G ; D |- letBBox x = t1 in t2 : B

  G ; D |- t : A
  ---------------------- :: diaI
  G ; D |- Dia t : Dia A

  G ; D |- t1 : Dia A && G ; x : A |- t2 : Dia B
  ---------------------------------------------- :: diaE
  G ; D |- letDia x = t1 in t2 : Dia B

defn
  t1 ~> t2 :: :: red :: r_   
by

  ------------------------ :: beta
  (\x:A.t2) t1 ~> [t1/x]t2

  ------------------- :: first
  fst (t1 , t2) ~> t1

  ------------------- :: second
  snd (t1 , t2) ~> t2

  ------------------------------------- :: case1
  case (inj1 t) of x.t1,x.t2 ~> [t/x]t1

  ------------------------------------- :: case2
  case (inj2 t) of x.t1,x.t2 ~> [t/x]t2

  ----------------------------------- :: box
  letBox x = Box t1 in t2 ~> [t1/x]t2

  ------------------------------------- :: bdia
  letBDia x = BDia t1 in t2 ~> [t1/x]t2

  ------------------------------------- :: bbox
  letBBox x = BBox t1 in t2 ~> [t1/x]t2

  ------------------------------------- :: dia
  letDia x = Dia t1 in t2 ~> [t1/x]t2

  ---------------------------------------------------------------------------- :: boxBox
  letBox x = letBox y = t1 in t2 in t3 ~> letBox y = t1 in letBox x = t2 in t3

  ------------------------------------------------------------------------------- :: bdiaBdia
  letBDia x = letBDia y = t1 in t2 in t3 ~> letBDia y = t1 in letBDia x = t2 in t3

  -------------------------------------------------------------------------------- :: boxBBox
  letBBox x = letBBox y = t1 in t2 in t3 ~> letBBox y = t1 in letBBox x = t2 in t3

  ---------------------------------------------------------------------------- :: diadia
  letDia x = letDia y = t1 in t2 in t3 ~> letDia y = t1 in letDia x = t2 in t3